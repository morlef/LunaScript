# LunaScript

## 1. 概要

本プログラムは、luna3419のインディーゲーム制作の行く先々でアドベンチャー部の実装の記法で揉めるため、<br>とりあえず作ってMITライセンスで公開しておけばいいのではという発想に基づいたものである。

## 2. 基本ルール

* ファイル形式: UTF-8 エンコーディングのテキストファイル (拡張子推奨: .txt)。  
* 行処理: 1行を1コマンドまたは1テキストブロックとして処理する。テキスト類の改行コードは\n。  
* コメント: // で始まる行、または行の途中から // 以降はコメントとして扱い、ゲーム動作には影響しない。  
* 空行: 無視される (可読性のために自由に入れてよい) 。

## 3. テキストと会話

ゲームの主体となるテキスト表示に関する記法。

### 3.1. ナレーション（地の文）

特別な記号をつけずに記述された行は、メッセージウィンドウに表示されるテキストとして扱う。

```
雨が降り続いていた。  
僕は傘も差さずに立ち尽くしている。 // 改行ではなく次のテキスト
```

### 3.2. キャラクターのセリフ

\[名前\] で行を開始することで、その行を特定キャラクターのセリフとして扱う。  
名前ブラケットの直後にテキストを記述する。  

```
[主人公] "もう、戻れないのかな。"  
[謎の少女] "ううん、まだ始まりに過ぎないわ。"
```

* 仕様上の注意: 表示時は \[\] 内を名前欄、それ以降をメッセージ欄に表示する。

## 4. システムコマンド

演出やゲームの挙動を制御する命令。すべてのコマンドは @ で開始する。  
パラメータは半角スペースで区切る。

### 4.1. 背景操作

* 構文: @bg {ファイル名} {フェード時間(ミリ秒):省略可}  
* 説明: 背景画像を変更する。

```
@bg classroom_day // assets/bg/classroom_day.[png, jp(e)g, bmp等] を参照  
@bg classroom_sunset 1000  // 1秒かけて切り替え
```

* 特殊構文: @bg _
* 説明: 背景を透明にする。テキストボックスとキャラクターのみ表示して3D背景のチュートリアルを行うなど。

### 4.2. 立ち絵 (キャラクター画像) 操作

* 登場/変更: @chara\_in {キャラID} {表情/ファイル名} {x位置} {y位置} {フェード時間:省略可}  
* 退場: @chara\_out {キャラID} {フェード時間:省略可}

```
// 画面中央に太郎の笑顔を表示  
@chara_in taro smile center 60% // assets/characters/taro_smile.[png, jp(e)g, bmp等] を参照  

// 太郎を退場させる  
@chara_out taro
```

位置定義例: left, center, right, top, bottom, もしくはpxや%で指定 (何もつけなかった場合pxとみなされる)。

### 4.3. 画像操作

* 登場/変更: @image\_in {ID} {x位置} {y位置} {フェード時間:省略可}    
* 退場: @image\_out {ID} {フェード時間:省略可}

実装はキャラクターと同じだが、可読性のため分けている。また、差分がない。  
参照は assets/characters/{ID}.\[png, jp(e)g, bmp等\]

### 4.4. サウンド操作

* BGM再生: @bgm {ファイル名} {ループ:true/false} {フェード時間:省略可}  
* BGM停止: @stop\_bgm {フェード時間:省略可}  
* SE再生: @se {ファイル名}

```
@bgm daily_life true // assets/bgm/daily_life.[wav, mp3, ogg等] を参照  
@se door_open // assets/se/door_open.[wav, mp3, ogg等] を参照
```

### 4.5. 演出・待機

* 待機: @wait {ミリ秒}  
* 画面揺れ: @shake {時間} {強さ}
* 画面フラッシュ: @flash {色コード} {時間}
* クリック待ち: @wait_click

実装が適当なので、強さはいくらでも増やせる。プレビューを見ていい感じに

```
@shake 500 2
@flash #FFFFFF 200
@wait 1000
```

### 4.6. オブジェクト演出
表示中のキャラクターや画像 (ID指定) に対して、個別の演出を行う。

* 移動: @move {ID} {x位置} {y位置} {時間} {イージング:省略可}
* 拡大縮小: @scale {ID} {x倍率%} {y倍率%} {時間}
* 回転: @rotate {ID} {角度} {時間}
* 個別揺れ: @shake_obj {ID} {時間} {強さ}
* 色調変更: @tint {ID} {色コード} {時間}

```
// 太郎が驚いて飛び跳ねる
@move taro center 70% 200 ease-out
@wait 200
@move taro center 60% 200 ease-in

// 暗転したように見せるためキャラを黒くする
@tint taro #000000 500
```

## 5. フロー制御

シナリオの分岐、選択肢、ジャンプに関する記法。

### 5.1. ラベル

ジャンプ先となる地点を示すマーカー。\* で開始する。

`*scene_rooftop`

### 5.2. ジャンプ

指定したラベルへ強制移動する。

* 構文: @goto {ラベル名}

`@goto scene_rooftop`

### 5.3. 選択肢

プレイヤーに入力を求める分岐処理。  
@choice ブロックで定義し、選択肢テキストとジャンプ先ラベルを \>\> で結ぶ。  
```
@choice  
"屋上へ行く" >> *goto_rooftop  
"教室に残る" >> *goto_classroom  
@end\_choice
```

### 5.4. スクリプトジャンプ

スクリプトの読み込みはエンジン側(Unity等)で管理するためあまりないと思うが、念の為。

* 構文: @script {スクリプトファイル名} {ラベル名:省略可}

`@script test_scenario.txt rooftop`

## 6. 変数と条件分岐 (フラグ管理)

### 6.1. 変数操作

変数は $ で開始する。

* 構文: @set {$変数名} \= {値}
* 構文: @set {$変数名} \+= {値} // 文字列の場合後ろにつながる
* 構文: @set {$変数名} \-= {値} // 文字列の場合前につながる

```
@set $love_point = 5  
@set $love_point += 1
@set $has_key = true
```

### 6.2. 条件分岐

変数の値によってルートを変える。

* 構文: @if {$変数} {演算子} {値} 〜 @endif

```
@if $love_point >= 10  
    [ヒロイン] "大好きだよ！"  
    @bgm happy_end  
@else  
    [ヒロイン] "……さようなら。"  
    @bgm bad_end  
@endif
```

## 7. その他細々とした仕様について

### 7.1. イージング

基本的にタグの時間指定の箇所を {ミリ秒}@{イージングタイプ} とするとイージングがかかる。  
内部的には数値の増え方にイージングがかかる脳筋実装のため、効果のないエフェクトもある。

イージングの種類は以下の通り。
```
ease-in
ease-out
ease-in-out
linear
back-in-out
```

### 7.2. リッチテキストタグ

以下のリッチテキストタグが使用可能。<br>
`<b>`, `<i>`, `<u>`, `<s>`, `<color>`, `<size>`


使用するエンジン次第 (例: UnityのTextMeshPro) でより多くのタグに対応。